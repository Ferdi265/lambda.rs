# type lambda : arg -> res
# res can be anything

# identity lambda
ident = a -> a


# y fixed point combinator
# used to build recursive functions
y = g -> (f -> f f) f -> g x -> f f x


# type bool : a -> b -> res
# res is either a or b

# the two possible bools
true = a -> b -> a
false = a -> b -> b

# unary bool combinators
not = a -> a false true
always = a -> true
never = a -> false

# binary bool combinators
and = a -> b -> a b false
or = a -> b -> a true b
xor = a -> b -> a (not b) b
nand = a -> b -> not (and a b)
nor = a -> b -> not (or a b)
xnor = a -> b -> not (xor a b)

# utility function for type conversion loops
# usage: until2 cond f_iter f_val i_iter i_val
until2 = cond -> fiter -> fval -> iiter -> ival -> (y until2 ->
    iiter -> ival -> cond iiter (x -> ival) (x -> until2 (fiter iiter) (fval ival)) ident
) iiter ival

# type tuple : bool -> res
# container type, internally stores two values
# returns the first one on true
# returns the second one on false

# creates a tuple containing a and b
tuple = a -> b -> sel -> sel a b

# select the first element of the tuple
1st = tup -> tup true
# select the second element of the tuple
2nd = tup -> tup false


# chain multiple actions
# never returns a value
do = y do -> arg -> f -> do (f arg)


# type list : ((first, empty), rest))
# linked-list container type built from tuples

# the empty list
nil = tuple (tuple ident true) ident

# prepends to a list
prepend = a -> list -> tuple (tuple a false) list

# checks if the list is empty
empty = list -> 2nd (1st list)

# get the first element of the list
# returns the first element of the list
# returns ident if the list is empty
first = list -> 1st (1st list)

# get the rest of the list
# returns a new list of everything except the first element
# returns an empty list (nil) if the list is empty
rest = list -> empty list nil (2nd list)


# apply a function to each element of a list
# returns a new list with every element being the result of the function called with the old element
map = y map -> f -> list -> empty list (x -> nil) (x -> prepend (f (first list)) (map f (rest list))) ident

# accumulate a result by applying a function to successive list elements
# left-associative and right-associative variants
foldl = y foldl -> f -> initial -> list -> empty list (x -> initial) (x -> foldl f (f initial (first list)) (rest list)) ident
foldr = y foldr -> f -> initial -> list -> empty list (x -> initial) (x -> f (first list) (foldr f initial (rest list))) ident


# type nat : (iszero, pred)
# define naturals as lists without content

# analogous to nil
zero = tuple true ident
# analogous to empty
iszero = nat -> 1st nat
# analogous to prepend
succ = nat -> tuple false nat
# analogous to rest
pred = nat -> 2nd nat

# call f on an input n times
count = y count -> f -> initial -> nat -> iszero nat (x -> initial) (x -> count f (f initial) (pred nat)) ident

# addition and subtraction
add = y add -> nat1 -> nat2 -> iszero nat2 (x -> nat1) (x -> add (succ nat1) (pred nat2)) ident
sub = y sub -> nat1 -> nat2 -> iszero nat2 (x -> nat1) (x -> sub (pred nat1) (pred nat2)) ident

# multiplication
mul = nat1 -> nat2 -> (y mul -> res -> iter -> iszero iter (x -> res) (x -> mul (add res nat1) (pred iter)) ident) zero nat2

# comparison
less = y less -> nat1 -> nat2 -> iszero nat1 (x -> not (iszero nat2)) (iszero nat2 (x -> false) (x -> less (pred nat1) (pred nat2))) ident
equal = y equal -> nat1 -> nat2 -> iszero nat1 (x -> iszero nat2) (iszero nat2 (x -> false) (x -> equal (pred nat1) (pred nat2))) ident
greater = nat1 -> nat2 -> less nat2 nat1


# bubble sort
trivially_sorted = list ->
    (empty list
        (x -> true)
        (x -> empty (rest list))
    ident)

sorted = y sorted -> list ->
    (trivially_sorted list
        (x -> true)
    (x -> less (first list) (first (rest list))
        (x -> sorted (rest list))
        (x -> false)
    ident) ident)

bubble_iter = y bubble_iter -> list ->
    (trivially_sorted list
        (x -> list)
    (x -> less (first list) (first (rest list))
        (x -> prepend (first list) (bubble_iter (rest list)))
        (x -> prepend (first (rest list)) (bubble_iter (prepend (first list) (rest (rest list)))))
    ident) ident)

bubble_sort = y bubble_sort -> list ->
    (sorted list
        (x -> list)
        (x -> bubble_sort (bubble_iter list))
    ident)


# convenience number literals
0 = zero
1 = succ 0
2 = succ 1
3 = succ 2
4 = succ 3
5 = succ 4
6 = succ 5
7 = succ 6
8 = succ 7
9 = succ 8
10 = succ 9

# I/O
# interaction with the outside world happens solely by passed-in lambdas
# that have side effects

# the 'standard' set of simple I/O facilities is passed as a list of lambdas
# each (exept the first) taking a single argument and returning a single value
#
# [IOzero, IOsucc, IOpred, IOiszero, IOgetchar, IOputchar]

# the IO integer zero
IOzero = IO -> first IO

# increments the passed IO integer
IOsucc = IO -> first (rest IO)

# decrements the passed IO integer
IOpred = IO -> first (rest (rest IO))

# checks if the passed IO integer is zero
# returns a boolean function (a -> b -> ?)
IOiszero = IO -> first (rest (rest (rest IO)))

# reads a character of input
# ignores its argument
# returns the character + 1 as an IO integer on success
# returns 0 as an IO integer on error or EOF
IOgetchar = IO -> first (rest (rest (rest (rest IO))))

# writes a character of output
# writes the passed IO integer - 1 to the output
# returns an identity function
IOputchar = IO -> first (rest (rest (rest (rest (rest IO)))))


# IO integer to nat conversion functions
IO2nat = IO -> n -> until2 (IOiszero IO) (IOpred IO) succ n zero
nat2IO = IO -> n -> until2 iszero pred (IOsucc IO) n (IOzero IO)

# IO character to nat conversion functions
isEOF = IO -> c -> iszero (IO2nat IO c)
IOC2nat = IO -> c -> pred (IO2nat IO c)
nat2IOC = IO -> c -> nat2IO IO (succ c)

# printing function
puts = IO -> s -> map (c -> IOputchar IO (nat2IOC IO c)) s


dec2 = a -> b -> (add (mul a 10) b)
dec3 = a -> b -> c -> (add (mul (dec2 a b) 10) c)

main = IO -> (do ident
    (x -> puts IO
        (prepend (dec2 7 2)
        (prepend (dec3 1 0 1)
        (prepend (dec3 1 0 8)
        (prepend (dec3 1 0 8)
        (prepend (dec3 1 1 1)
        (prepend (dec2 1 0)
            nil))))))
    )
    (x -> puts IO
        (prepend (dec2 8 7)
        (prepend (dec3 1 1 1)
        (prepend (dec3 1 1 4)
        (prepend (dec3 1 0 8)
        (prepend (dec3 1 0 0)
        (prepend (dec2 1 0)
            nil))))))
    )
)

start = IOzero -> IOsucc -> IOpred -> IOiszero -> IOgetchar -> IOputchar -> main (
    prepend IOzero (
    prepend IOsucc (
    prepend IOpred (
    prepend IOiszero (
    prepend IOgetchar (
    prepend IOputchar (
        nil))))))
)
